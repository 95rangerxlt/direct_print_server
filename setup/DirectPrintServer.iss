; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Direct Print Server"
#define MyAppVersion "0.1-alpha.10"
#define MyAppPublisher "JSC Solvaig"
#define MyAppURL "https://github.com/procks/direct_print_server"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{215ED57E-4DC9-40EA-85E2-4A326D2C0419}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\DirectPrintServer
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
OutputDir=Output
OutputBaseFilename=DirectPrintServerSetup
Compression=lzma
SolidCompression=yes
DisableWelcomePage=no
CloseApplications=force

[Languages]
Name: "en"; MessagesFile: "compiler:Default.isl"
Name: "ru"; MessagesFile: "compiler:Languages\Russian.isl"
Name: "uk"; MessagesFile: "compiler:Languages\Ukrainian.isl"

[CustomMessages]
en.InstallingService=Installing service "Direct Print Service"
en.UninstallingService=Uninstalling service "Direct Print Service"
en.ServiceAccountInformation=Log on "Direct Print Service" as:
en.EnterAccountInformation=Please enter account information
en.LocalSystemAccount=Local system account (shared printers not available)
en.User=User
en.Password=Password
en.ConfirmPassword=Confirm password
en.PleaseEnterUser=Please enter User
en.PleaseEnterPass=Please enter Password
en.PasswordNotMatch=Password does not match the confirm password
en.Configure=Configure Service %1
en.Monitor=Monitor Service %1
en.StartService=Start Service %1
en.StopService=Stop Service %1
en.Start=Start %1
en.Stop=Stop %1

ru.InstallingService=Установка службы "Direct Print Service"
ru.UninstallingService=Удаление службы "Direct Print Service"
ru.ServiceAccountInformation=Запускать сервис "Direct Print Service" от имени:
ru.EnterAccountInformation=Пожалуйста, введите данные учетной записи
ru.LocalSystemAccount=С системной учетной записью (общие принтеры не доступны)
ru.User=Пользователь
ru.Password=Пароль
ru.ConfirmPassword=Подтвердите пароль
ru.PleaseEnterUser=Введите имя пользователя
ru.PleaseEnterPass=Введите пароль
ru.PasswordNotMatch=Несовпадение паролей
ru.Configure=Configure Service %1
ru.Monitor=Monitor Service %1
ru.StartService=Start Service %1
ru.StopService=Stop Service %1
ru.Start=Start %1
ru.Stop=Stop %1

uk.InstallingService=Встановлення служби "Direct Print Service"
uk.UninstallingService=Видалення служби "Direct Print Service"
uk.ServiceAccountInformation=Запускати сервіс "Direct Print Service" від імені:
uk.EnterAccountInformation=Будь ласка, введіть дані облікового запису
uk.LocalSystemAccount=З системним обліковим записом (загальні принтери не доступні)
uk.User=Користувач
uk.Password=Пароль
uk.ConfirmPassword=Підтвердіть пароль
uk.PleaseEnterUser=Введіть ім'я користувача
uk.PleaseEnterPass=Введіть пароль
uk.PasswordNotMatch=Неспівпадіння паролів
uk.Configure=Configure Service %1
uk.Monitor=Monitor Service %1
uk.StartService=Start Service %1
uk.StopService=Stop Service %1
uk.Start=Start %1
uk.Stop=Stop %1

[Files]
Source: "Files\gsdll32.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "Files\gswin32c.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "Files\LICENSE.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "Files\DirectPrintServer.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "Files\DirectPrintServer.exe.manifest"; DestDir: "{app}"; Flags: ignoreversion
Source: "Files\stop.ico"; DestDir: "{app}"; Flags: ignoreversion
Source: "Files\play.ico"; DestDir: "{app}"; Flags: ignoreversion
Source: "Files\printer.ico"; DestDir: "{app}"; Flags: ignoreversion

; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Components]
Name: "program"; Description: "Program Files"; Types: full compact custom; Flags: fixed
Name: "autostart"; Description: "Autostart"; Types: full
Name: "autostart\sturtup"; Description: "SturtUp"; Flags: exclusive;
Name: "autostart\service"; Description: "Service"; Flags: exclusive;

[Tasks]
Name: desktopicon; Description: "Create a &desktop icon"; GroupDescription: "Additional icons:"; Components: not autostart\service; Flags: unchecked
;Name: startAutorun; Description: "Запустить сервер"; Components: autostart\sturtup

[Icons]
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
;Name: "{group}\{cm:StartService,{#MyAppName}}"; Filename: "{app}\DirectPrintServer.exe"; IconFilename: {app}\printer.ico; Parameters: "start_s"; Components: not autostart\sturtup
;Name: "{group}\{cm:StopService,{#MyAppName}}"; Filename: "{app}\DirectPrintServer.exe"; IconFilename: {app}\printer.ico; Parameters: "stop_s"; Components: not autostart\sturtup
Name: "{group}\{#MyAppName}"; Filename: "{app}\DirectPrintServer.exe"; IconFilename: {app}\printer.ico; Components: not autostart\service
Name: "{commonstartup}\{#MyAppName}"; Filename: "{app}\DirectPrintServer.exe"; IconFilename: {app}\printer.ico; Components: autostart\sturtup
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\DirectPrintServer.exe"; IconFilename: {app}\printer.ico; Components: not autostart\service; Tasks: desktopicon

[Run]
Filename: "netsh"; Parameters: "firewall add portopening TCP 9188 ""Direct Print Service"" ENABLE ALL"; Flags: runhidden
Filename: "netsh"; Parameters: "firewall add portopening UDP 9188 ""Direct Print Service"" ENABLE ALL"; Flags: runhidden
Filename: "{app}\DirectPrintServer.exe"; Parameters: "install_s"; WorkingDir: "{app}"; Flags: runhidden; StatusMsg: "{cm:InstallingService}"; Components: autostart\service
Filename: "{app}\DirectPrintServer.exe"; Parameters: "start_s"; WorkingDir: "{app}"; Flags: runhidden; StatusMsg: "{cm:InstallingService}"; Components: autostart\service
Filename: "{app}\DirectPrintServer.exe"; WorkingDir: "{app}"; Flags: runasoriginaluser nowait; Components: autostart\sturtup

[UninstallRun]
Filename: "{app}\DirectPrintServer.exe"; Parameters: "remove_s"; WorkingDir: "{app}"; Flags: runhidden; StatusMsg: "{cm:UninstallingService}"
Filename: "taskkill"; Parameters: "/f /im DirectPrintServer.exe"; Flags: runhidden

[Code]
procedure CurStepChanged(CurStep: TSetupStep);
var
  id: string;
  WinHttpReq: Variant;
begin
  if CurStep = ssPostInstall then
  begin
    try
      id := IntToStr(Random(999999));
      WinHttpReq := CreateOleObject('WinHttp.WinHttpRequest.5.1');
      WinHttpReq.Open('GET', 'http://www.google-analytics.com/collect?v=1&tid=UA-74107078-1&cid=' + id + '&t=event&ec=Action&ea=Install', false);
      WinHttpReq.SetRequestHeader('Content-Type', 'application/x-www-form-urlencoded');
      WinHttpReq.Send();
      // WinHttpReq.ResponseText will hold the server response
    except
    end;
  end;
end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
var
  id: string;
  WinHttpReq: Variant;
begin
  if CurUninstallStep = usPostUninstall then
  begin
    try
      id := IntToStr(Random(999999));
      WinHttpReq := CreateOleObject('WinHttp.WinHttpRequest.5.1');
      WinHttpReq.Open('GET', 'http://www.google-analytics.com/collect?v=1&tid=UA-74107078-1&cid=' + id + '&t=event&ec=Action&ea=Uninstall', false);
      WinHttpReq.SetRequestHeader('Content-Type', 'application/x-www-form-urlencoded');
      WinHttpReq.Send();
      // WinHttpReq.ResponseText will hold the server response
    except
    end;
  end;
end;